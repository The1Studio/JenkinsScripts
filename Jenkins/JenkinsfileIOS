@Library('jenkins-helper') _

def jenkinsBuilder = new UnityIOSJenkinsBuilder(this)

pipeline {
    agent { label "macos" }

    stages {
        stage('Create Builder') {
            steps {
                script {
                    jenkinsBuilder
                            .loadResource()
                            .importBuildSettings([jobName                    : env.JOB_NAME,
                                                  buildName                  : env.PARAM_BUILD_FILE_NAME,
                                                  buildNumber                : env.BUILD_NUMBER,
                                                  buildVersion               : env.BUILD_VERSION,
                                                  platform                   : 'ios',
                                                  scriptingBackend           : 'il2cpp',
                                                  unityScriptingDefineSymbols: env.PARAM_UNITY_SCRIPTING_DEFINE_SYMBOLS,
                                                  unityIdEmail               : credentials('jenkins-id-for-unity-email'),
                                                  unityIdPassword            : credentials('jenkins-id-for-unity-password'),
                                                  unityIdLicense             : credentials('jenkins-id-for-unity-license'),
                                                  uploadDomain               : env.PARAM_BUILD_DOWNLOAD_URL,
                                                  discordWebhookUrl          : env.PARAM_DISCORD_WEBHOOK_URL,
                                                  rootPathAbsolute           : pwd(),
                                                  unityProjectPathAbsolute   : "${pwd()}/Unity${env.PARAM_BUILD_FILE_NAME}",
                                                  unityBinaryPathAbsolute    : tool(name: env.PARAM_UNITY_TOOL_NAME),
                                                  isBuildDevelopment         : env.PARAM_SHOULD_BUILD_DEVELOPMENT == 'true',
                                                  isOptimizeBuildSize        : env.PARAM_SHOULD_OPTIMIZE_BUILD_SIZE == 'true',
                                                  isNotifyToChatChannel      : env.PARAM_SHOULD_NOTIFY_TO_CHAT_CHANNEL == 'true',
                                                  signingTeamId              : env.PARAM_SIGNING_TEAM_ID,])
                }
            }
        }

        stage("Clean and sync") {
            options { timeout(time: 1, unit: 'HOURS') }

            steps {
                script {
                    echo "---- Start cleaning ----"
                    sh "git clean -fd"
                    sh "git reset --hard"
                    sh "git submodule foreach --recursive git clean -fd"
                    sh "git submodule foreach --recursive git reset --hard"
                    sh "rm -rf ./Build/"
                    echo "---- End cleaning   ----"

                    echo "---- Update submodule ----"
                    sh "git submodule update --init"
                    sh "git submodule update"
                    echo "---- End Update submodule   ----"

                    // set build version and other info
                    sh "JenkinsScripts/BashScripts/set-game-version.sh '${env.BUILD_VERSION}' '${env.BUILD_NUMBER}' '${env.GIT_COMMIT}' '${env.PARAM_BUILD_FILE_NAME ?: 'TheOneStudio'}'"
                }
            }
        }

        stage("Build Client (IOS)") {
            options { timeout(time: 2, unit: 'HOURS') }

            steps {
                script {
                    jenkinsBuilder.build()
                }
            }

            post {
                failure {
                    script {
                        jenkinsBuilder.jenkinsUtils.runCommand('git clean -fd')
                        jenkinsBuilder.jenkinsUtils.runCommand('git reset --hard')
                        jenkinsBuilder.jenkinsUtils.runCommand('git submodule foreach --recursive git reset --hard')

                        if (jenkinsBuilder.settings.buildName) {
                            if (isUnix()) {
                                sh "rm -rf ./Unity${jenkinsBuilder.settings.buildName}/Library/Bee/"
                            } else {
                                powershell script: "Remove-Item -Recurse -Force .\\Unity${jenkinsBuilder.settings.buildName}\\Library\\Bee\\"
                            }
                        }
                    }
                }

                always {
                    script {
                        String logPath = jenkinsBuilder.getLogPath(false) { "Build-Client.${jenkinsBuilder.settings.platform}.log" }
                        String reportPath = jenkinsBuilder.getLogPath(false) { "Build-Client-Report.${jenkinsBuilder.settings.platform}.log" }

                        if (fileExists(logPath)) {
                            archiveArtifacts artifacts: logPath, allowEmptyArchive: true
                        }

                        if (fileExists(reportPath)) {
                            archiveArtifacts artifacts: reportPath, allowEmptyArchive: true
                        }
                    }
                }
            }
        }

        stage("Upload Build") {
            options { timeout(time: 75, unit: 'MINUTES') }

            steps {
                script {
                    jenkinsBuilder.uploadBuild()
                }
            }
        }
    }

    post {
        always {
            script {
                jenkinsBuilder.notifyToChatChannel()
            }
        }
    }
}
